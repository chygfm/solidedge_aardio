import win.ui;
import win.ui.grid;
import win.ui.accelerator;
import win.ui.listPopup;
import access;
import fsys;
import fsys.table;
import win.clip;
//import win.ui.menu;
//import win.util.tray;
/*DSG{{*/
mainForm = win.form(text="QHC物料查询";right=638;bottom=714)
mainForm.add(
button={cls="button";text="查询";left=521;top=7;right=620;bottom=42;default=1;dr=1;dt=1;tabstop=1;z=2};
button2={cls="button";text="安装驱动";left=14;top=687;right=100;bottom=709;db=1;dl=1;hide=1;z=5};
editUser={cls="edit";left=244;top=11;right=512;bottom=40;aw=1;dr=1;dt=1;edge=1;tabstop=1;z=1};
listview={cls="listview";left=14;top=56;right=621;bottom=682;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;z=4};
static={cls="static";text="Static";left=18;top=686;right=620;bottom=714;align="right";aw=1;center=1;color=12632256;db=1;dl=1;dr=1;font=LOGFONT(h=-11);transparent=1;z=3}
)
/*}}*/

/*只允许一个实例{{*/
import win.ui.atom;
var atom,hwnd = mainForm.atom("domisoft.aardio.qtip");
if(!atom){ 
	win.showForeground(hwnd);
	win.quitMessage();	
	return;
}
/*}}*/

var grid = win.ui.grid(mainForm.listview);//创建数据视图
var logInput=table.array()
var dbpath = (function(){
		var xlsxlist=fsys.list("\",,"系统所有物料*.xlsx")
		if (#xlsxlist>=1) {
			return xlsxlist[xlsxlist[#xlsxlist]] 
		}else{
	    	win.msgbox("没找到物料数据文件:系统所有物料*.xlsx")
	    	return null;
		}
	})()


var db,err = access(dbpath)

if (err){
	win.msgbox(err)
	mainForm.button2.hide=0;
}

var setting = fsys.table(fsys.getTempDir()++"/domisoft.qtip.setting.table") 
setting.load(); //从文件载入表
if !(setting.inputlog) setting.inputlog=table.array()

mainForm.button.oncommand = function(id,event){
    if !(dbpath) return false; 
    grid.clear()
    var userinput=string.trim(mainForm.editUser.text)
    try{	//保存进历史清单
        if !(table.find(setting.inputlog,userinput)){
    		table.unshift(setting.inputlog,userinput)
    		if table.len(setting.inputlog>15) table.pop(setting.inputlog)
    		setting.save()
        }
    }
    if string.len(userinput)==0 return false
    table.push(logInput,userinput)
    if (string.startWith(userinput,"008",true)) userinput="H" ++ userinput
    userinput=string.replace(userinput,"@%","\%")
    userinput=string.replace(userinput,"@_","\_")
    userinput=string.replace(userinput,"@*","%")
    userinput=string.replace(userinput,"@?","_")    
    var searchField;	
    if (string.startWith(userinput,"H008",true) || string.startWith(userinput,"BG",true)) {
    	searchField="物料";
	}else{
    	searchField="物料描述";
	}
	sqlstr="SELECT [物料], [物料描述],[物料类型],[跨工厂物料状态] AS [禁用] FROM [Sheet1$] WHERE [" ++ searchField ++"] LIKE '" ++ userinput ++"'";

	var tab = db.getTable(sqlstr);
	grid.setTable( tab )
	
//调整列宽		
	mainForm.listview.setColumn({cx=100;fmt=0x2/*_LVCFMT_CENTER*/},1)
	mainForm.listview.setColumn({cx=380;fmt=0x0/*_LVCFMT_LEFT*/},  2)
	mainForm.listview.setColumn({cx=70 ;fmt=0x2/*_LVCFMT_CENTER*/},3)
	mainForm.listview.setColumn({cx=50 ;fmt=0x2/*_LVCFMT_CENTER*/},4)
	
}

/*onclose{{*/
mainForm.onClose = function(hwnd,message,wParam,lParam){
	if (db) db.close();	
}
/*}}*/

/*CTRLC复制模块{{*/
var accelerator = win.ui.accelerator({
    { 
        ctrl = true; vkey = 'C'#; 
        oncommand = function() {
            if (grid.editBox.hide==1){
            	var tab=table.array();
            	for (i=1;#mainForm.listview.selected;1){
                	table.push(tab,string.join(mainForm.listview.items[mainForm.listview.selected[i]],'\t'))
            	}           
				win.clip.write(string.join(tab,'\n'))
			}else {	//编辑状态直接调用copy
				grid.editBox.copy()
			}
			
			
        }; 
    };//  {more}
    
},mainForm );
/*}}*/

/*检测并安装驱动{{*/
mainForm.button2.oncommand = function(id,event){
	import access.oleDb12;
	access.oleDb12.install();//检测并安装Microsoft.Ace.OLEDB.12驱动
}
/*}}*/
mainForm.editUser.onOk = function(id,event){ 
	mainForm.button.oncommand();
    return true;	
}
/*editUser弹出提示{{*/
var listPopup = win.ui.listPopup(mainForm.editUser);

listPopup.onGetItems = function(){
	var items = setting.inputlog;
	if(!#items) return;
	var selIndex = 1;
	reduce(items,function(prev,next,index){ 
		if( #next < #prev ) { selIndex = index; return next };
		return prev
	})
	return items,selIndex;
}

/*}}*/

/*listview事件{{*/
mainForm.listview.onnotify = function(id,code,ptr){
    select(code) {
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {
			var frmChild = mainForm.loadForm("\dlg\findRelatedDoc.aardio");
			frmChild.show();
			return false; 
    	}
    }
}
/*}}*/




mainForm.static.text=dbpath;
mainForm.show();
mainForm.editUser.setFocus()
win.loopMessage();