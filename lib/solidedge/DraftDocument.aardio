//DraftDocument 常用函数
namespace solidedge;
namespace DraftDocument{
	getTitleBlock = function(seSht){
        for (i = 1;  seSht.BlockOccurrences.Count ) { //获取Titleblock
            select  (seSht.BlockOccurrences.Item(i).Block.Name){
                case "Title 2020", "Title", "Title-SRDC_V1", "Title-SRDC" {
                    var titleblk = seSht.BlockOccurrences.Item(i);
                    return titleblk ;
                }
            }
        }
	}
	getFirstSheet = function(dft){
		for (i=1; dft.Sheets.Count; 1){
			if (dft.Sheets.item(i).SectionType == 0 /* igWorkingSection */)
				if (dft.Sheets.item(i).Number==1)
					return dft.Sheets.item(i);
		}
	}
	
	getLabelTable = function(blk){
		var seLbs=blk.BlockLabelOccurrences		
		var tar={}
		for (i=1;seLbs.count){
    		tar[seLbs.item(i).name]=..string.trim(seLbs.item(i).value)
    		tar["零件名称"] := tar["名称(中文)"] 
		}		
		return tar; 
	}
	getValueFromBlk = function(blk, lableName){
		var seLbs=blk.BlockLabelOccurrences		
		for (i=1;seLbs.count){
    		if (seLbs.item(i).name==lableName) return seLbs.item(i).value
		}
	}
	getVerFromShtCorner = function(seSht){
		var	width = seSht.SheetSetup.SheetWidth * 1000;
        var txts = seSht.TextBoxes        
        for (i = 1 ; txts.Count){
            var x,y,z=txts.Item(i).GetOrigin( )
            //Debug.Print txts.Item(i).Text, x * 1000, y * 1000, z * 1000
            if (..string.len(..string.trim(txts.Item(i).Text)) == 2) {
                if (x * 1000 > width * 0.94 && y * 1000 > 10) {
                    return ..string.trim(txts.Item(i).Text)
                }
            }
        }
	}
	getVerFromBlkCorner = function(blk){
        var txts = blk.Block.Views.Item(1).TextBoxes
        var txtbox = txts.Item(1)
        if (txts && txtbox) {
            var a,b,c=txtbox.GetOrigin ( )
            for (i = 2 ; txts.Count){
                var x,y,z=txts.Item(i).GetOrigin ()
                if (x > a || y < b) {
                    txtbox = txts.Item(i)
                    a,b,c=txtbox.GetOrigin ()
                }
            }
        }
        return ..string.trim(txtbox.Text)
	}
	setPdfOptions = function(seApp){
    	seApp.SetGlobalParameter ( 172 /*Sheet options*/, 1 /*All sheets*/ )
    	seApp.SetGlobalParameter ( 174 /*Use individual sheet sizes*/, true)
    	seApp.SetGlobalParameter ( 168 /*Save all colors as black*/, false)
	}
}
/**intellisense()
solidedge.DraftDocument = DraftDocument 支持库
solidedge.DraftDocument = !DraftDocument.
!DraftDocument.getTitleBlock(_seSht) = return title block object
!DraftDocument.getLabelTable(_blk) = return lables object
!DraftDocument.getValueFromBlk(_blk,_lblName) = return lable value by name
!DraftDocument.getVerFromShtCorner(_seSht) = return Ver value from sht rt_bt conrner
!DraftDocument.getVerFromBlkCorner(_blk) = return Ver value from title block def
!DraftDocument.setPdfOptions = set print All sheets, allow different sheet sizes, and allow color;
end intellisense**/